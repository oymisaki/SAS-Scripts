/* This session covers macro statement */

options mprint; /* print out sas codes generated by macros in log */

libname path "P:\SAS_Session\Session_3";

proc contents data = path.funda;
run;

proc print data = path.funda(obs = 20);
run;

data funda;
	set path.funda;
	array miss {5} at ch lt capx sstk;
	do i = 1 to dim(miss);
		if miss(i)=. then delete;
	end;
	drop i;
run;

proc contents data = path.comp;
run;

proc print data = path.comp(obs = 20);
run;

%let variables = gvkey datadate conm cusip cshom prccm prchm prclm;

data one;
	set path.comp(keep = &variables);
	array miss {3} prccm prchm prclm;
	do i = 1 to dim(miss);
		if miss(i)=. then delete;
	end;
	drop i;
run;

/* Positional Parameters */
%macro plot(dataset, year, yvar, xvar);

	data temp;
		set &dataset;
		if (fyear = &year);
	run;

	data tmp2;
		set temp(obs = 10);
	run;

	proc plot data = tmp2;
		plot &yvar*&xvar;
	run;

%mend;

%plot(funda, 1974, at, gvkey);
	

/* Keyword Parameters */

%macro sort(dsetin = , seq_sort = );

proc sort data = &dsetin;
	by &seq_sort;
run;

%mend;

%sort(dsetin = funda, seq_sort = at);
%sort(dsetin = funda, seq_sort = datadate descending gvkey);

%macro import(file = , output = test, dbms_opt = , num_rows = );

proc import datafile = &file out = &output dbms = &dbms_opt replace;
	guessingrows = &num_rows;
run;

%mend;

/* Run by both including and excluding the "output" option */

%import(file = "P:\SAS_Session\Session_3\sample_data.csv", dbms_opt = csv, num_rows = 1000);
%import(file = "P:\SAS_Session\Session_3\sample_data.csv", output = imp_out, dbms_opt = csv, num_rows = 1000);

/* example of macro conditional statements */

%macro conditional(weekday = );

%if &weekday = Monday %then %do;
	%import(file = "P:\SAS_Session\Session_3\sample_data.csv", output = d_out_&weekday., dbms_opt = csv, num_rows = 1000);
	%sort(dsetin = d_out_&weekday., seq_sort = datadate descending gvkey);
%end;
%else %if &weekday = Tuesday %then %do;
	%import(file = "P:\SAS_Session\Session_3\sample_data.csv", output = d_out_&weekday., dbms_opt = csv, num_rows = 1000);
	%sort(dsetin = d_out_&weekday., seq_sort = cusip);
%end;

%mend;

%conditional(weekday = Monday);
%conditional(weekday = Tuesday);

/* example of macro loop statements */

%macro loop(counter = );

%do i = 1 %to &counter.;
	%if &i. = 1 %then %do;
		%import(file = "P:\SAS_Session\Session_3\sample_data.csv", output = d_out_&i., dbms_opt = csv, num_rows = 1000);
		%sort(dsetin = d_out_&i., seq_sort = datadate descending gvkey);
	%end;
	%else %if &i. = 2 %then %do;
		%import(file = "P:\SAS_Session\Session_3\sample_data.csv", output = d_out_&i., dbms_opt = csv, num_rows = 1000);
		%sort(dsetin = d_out_&i., seq_sort = cusip);
	%end;
%end;

%mend;

%loop(counter = 2);
